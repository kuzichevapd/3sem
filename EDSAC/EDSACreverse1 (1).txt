 [вариант 9
 ТЗ: написать программу для выполнения реверса массива, используя Initial orders 1
 на вход подается количество итераций(перестановок пар), адреса первого и последнего элементов массив
 в ходе программы новый массив не появляется, только меняются местами элементы в исходном
 исходный массив: 1(ячейка 73), 2(ячейка 74), 3(ячейка 75), 5(ячейка 76),
 ожидаемый результат: 5(ячейка 73), 3(ячейка 74), 2(ячейка 75), 1(ячейка 76)]
























T [END:] 77 S [начало программы]
[LOOP: начало цикла] A [LEN:] 71 S [загрузка счетчика итераций]
S [CONST1:] 72 S [вычитание из счетчика константы 1]
G [END:] 66 S [если счетчик итераций меньше либо равен нулю, завершение программы]
T [LEN:] 71 S [загрузка обновленного значения счетчика итераций]
A [ADDR_START:] 69 S [загрузка в аккумулятор первого элемента пары]
L O L [сдвиг аккумулятора на один разряд влево]
A [TAKE:] 67 S [прибавляем код инструкции с нулевым полем адреса]
T [R1] 52 S [запись сформированной инструкции, обнуление аккумулятора]
A [ADDR_START:] 69 S [загрузка в аккумулятор первого элемента пары]
L O L [сдвиг аккумулятора на один разряд влево]
A [PUT:] 68 S [прибавляем код инструкции с нулевым полем адреса]
T [W1] 55 S [запись сформированной инструкции, обнуление аккумулятора]
A [ADDR_END:] 70 S [загрузка в аккумулятор второго элемента пары]
L O L [сдвиг аккумулятора на один разряд влево]
A [TAKE:] 67 S [прибавляем код инструкции с нулевым полем адреса]
T [R2] 54 S [запись сформированной инструкции, обнуление аккумулятора]
A [ADDR_END:] 70 S [загрузка в аккумулятор второго элемента пары]
L O L [сдвиг аккумулятора на один разряд влево]
A [PUT:] 68 S [прибавляем код инструкции с нулевым полем адреса]
T [W2] 57 S [запись сформированной инструкции, обнуление аккумулятора]
[R1:] A 0 S [загрузка в аккумулятор первого элемента из пары]
T 1 S [запись первого элемента из пары во вспомогательную ячейку 1]
[R2:] A 0 S [загрузка в аккумулятор второго элемента из пары]
[W1:] T 0 S [добавление первого элемента из пары]
A 1 S [взятие значение элемента из вспомогательной ячейки 1]
[W2:] T 0 S [добавление второго элемента из пары]
T 1 S [обнуление вспомогательной ячейки]
A [ADDR_START:] 69 S [добавление в аккумулятор адреса первого элемента из пары]
A [CONST1:] 72 S [прибавление единицы к нему]
T [ADDR_START:] 69 S [запись измененного адреса в ту же ячейку]
A [ADDR_END:] 70 S [добавление в аккумулятор адреса второго элемента из пары]
S [CONST1:] 72 S [вычитание из него единицы]
T [ADDR_END:] 70 S [запись измененного адреса в ту же ячейку]
E [LOOP_START:] 32 S [возвращение в начало цикла]
[END] Z 0 S [остановка программы]
[TAKE:] A 0 S [инструкция для забора числа из нужной ячейки]
[PUT:] T 0 S [инструкция для добавления числа в нужную ячейку]
[ADDR_START:] P 36 L [адрес первого элемента]
[ADDR_END:] P 38 S [адрес второго элемента]
[LEN:] P 1 S [счетчик итераций(количество необходимых перестановок пар)]
[CONST1:] P 0 L [константа 1 для итераций]
P 0 L [нулевой элемент массива, равный 1]
P 1 S [первый элемент массива, равный 2]
P 1 L [второй элемент массива, равный 3]
P 2 L [третий элемент массива, равный 5]






